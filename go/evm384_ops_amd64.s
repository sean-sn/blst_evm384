// Copyright Supranational LLC
// Licensed under the Apache License, Version 2.0, see LICENSE for details.
// SPDX-License-Identifier: Apache-2.0

#include "textflag.h"

TEXT ·add_mod_384(SB), NOSPLIT, $0-32
  MOVQ    a+8(FP), SI
  MOVQ    0(SI), R8
  MOVQ    8(SI), R9
  MOVQ    16(SI), R10
  MOVQ    24(SI), R11
  MOVQ    32(SI), R12
  MOVQ    40(SI), R13
  MOVQ    b+16(FP), DX
  ADDQ    0(DX), R8
  ADCQ    8(DX), R9
  ADCQ    16(DX), R10
  MOVQ    R8, R14
  ADCQ    24(DX), R11
  MOVQ    R9, R15
  ADCQ    32(DX), R12
  MOVQ    R10, AX
  ADCQ    40(DX), R13
  MOVQ    R11, BX
  SBBQ    DX, DX
  MOVQ    p+24(FP), CX
  SUBQ    0(CX), R8
  SBBQ    8(CX), R9
  MOVQ    R12, BP
  SBBQ    16(CX), R10
  SBBQ    24(CX), R11
  SBBQ    32(CX), R12
  MOVQ    R13, SI
  SBBQ    40(CX), R13
  SBBQ    $0, DX
  MOVQ    ret+0(FP), DI
  CMOVQCS R14, R8
  CMOVQCS R15, R9
  CMOVQCS AX, R10
  MOVQ    R8, 0(DI)
  CMOVQCS BX, R11
  MOVQ    R9, 8(DI)
  CMOVQCS BP, R12
  MOVQ    R10, 16(DI)
  CMOVQCS SI, R13
  MOVQ    R11, 24(DI)
  MOVQ    R12, 32(DI)
  MOVQ    R13, 40(DI)
  RET

TEXT ·sub_mod_384(SB), NOSPLIT, $0-32
  MOVQ  a+8(FP), SI
  MOVQ  0(SI), R8
  MOVQ  8(SI), R9
  MOVQ  16(SI), R10
  MOVQ  24(SI), R11
  MOVQ  32(SI), R12
  MOVQ  40(SI), R13
  MOVQ  b+16(FP), DX
  MOVQ  p+24(FP), CX
  SUBQ  0(DX), R8
  MOVQ  0(CX), R14
  SBBQ  8(DX), R9 
  MOVQ  8(CX), R15
  SBBQ  16(DX), R10
  MOVQ  16(CX), AX
  SBBQ  24(DX), R11
  MOVQ  24(CX), BX
  SBBQ  32(DX), R12
  MOVQ  32(CX), BP
  SBBQ  40(DX), R13
  MOVQ  40(CX), SI
  SBBQ  DX, DX
  ANDQ  DX, R14
  ANDQ  DX, R15
  ANDQ  DX, AX
  ANDQ  DX, BX
  ANDQ  DX, BP 
  ANDQ  DX, SI
  MOVQ  ret+0(FP), DI
  ADDQ  R14, R8
  ADCQ  R15, R9 
  MOVQ  R8, 0(DI)
  ADCQ  AX, R10
  MOVQ  R9, 8(DI)
  ADCQ  BX, R11
  MOVQ  R10, 16(DI)
  ADCQ  BP, R12
  MOVQ  R11, 24(DI)
  ADCQ  SI, R13
  MOVQ  R12, 32(DI)
  MOVQ  R13, 40(DI)
  RET

TEXT ·mul_mod_384(SB), NOSPLIT, $24-40
  MOVQ  b+16(FP), BX
  MOVQ  0(BX),DX
  MOVQ  a+8(FP), SI
  MOVQ  0(SI),R14
  MOVQ  8(SI),R15
  MOVQ  16(SI),AX
  MOVQ  24(SI),R12
  MOVQ  32(SI),DI
  MOVQ  40(SI),BP
  MOVQ  p+24(FP), CX
  LEAQ  -128(SI),SI
  LEAQ  -128(CX),CX
  MOVQ  inv+32(FP),R8
  MOVQ  R8,8(SP)

  MULXQ R14,R8,R9

  MULXQ R15,R14,R10
  MULXQ AX,R15,R11
  ADDQ  R14,R9
  MULXQ R12,AX,R12
  ADCQ  R15,R10
  MULXQ DI,DI,R13
  ADCQ  AX,R11
  MULXQ BP,BP,R14
  MOVQ  8(BX),DX
  ADCQ  DI,R12
  ADCQ  BP,R13
  ADCQ  $0,R14
  XORQ  R15,R15

  MOVQ  R8,16(SP)
  IMULQ 8(SP),R8


  XORQ  AX,AX
  MULXQ 0+128(SI),DI,BP
  ADOXQ DI,R9
  ADCXQ BP,R10

  MULXQ 8+128(SI),DI,BP
  ADOXQ DI,R10
  ADCXQ BP,R11

  MULXQ 16+128(SI),DI,BP
  ADOXQ DI,R11
  ADCXQ BP,R12

  MULXQ 24+128(SI),DI,BP
  ADOXQ DI,R12
  ADCXQ BP,R13

  MULXQ 32+128(SI),DI,BP
  ADOXQ DI,R13
  ADCXQ BP,R14

  MULXQ 40+128(SI),DI,BP
  MOVQ  R8,DX
  ADOXQ DI,R14
  ADCXQ BP,R15
  ADOXQ AX,R15
  ADOXQ AX,AX

  XORQ  R8,R8
  MULXQ 0+128(CX),DI,BP
  ADCXQ 16(SP),DI
  ADOXQ BP,R9

  MULXQ 8+128(CX),DI,BP
  ADCXQ DI,R9
  ADOXQ BP,R10

  MULXQ 16+128(CX),DI,BP
  ADCXQ DI,R10
  ADOXQ BP,R11

  MULXQ 24+128(CX),DI,BP
  ADCXQ DI,R11
  ADOXQ BP,R12

  MULXQ 32+128(CX),DI,BP
  ADCXQ DI,R12
  ADOXQ BP,R13

  MULXQ 40+128(CX),DI,BP
  MOVQ  16(BX),DX
  ADCXQ DI,R13
  ADOXQ BP,R14
  ADCXQ R8,R14
  ADOXQ R8,R15
  ADCXQ R8,R15
  ADOXQ R8,AX
  ADCXQ R8,AX
  MOVQ  R9,16(SP)
  IMULQ 8(SP),R9

  XORQ  R8,R8
  MULXQ 0+128(SI),DI,BP
  ADOXQ DI,R10
  ADCXQ BP,R11

  MULXQ 8+128(SI),DI,BP
  ADOXQ DI,R11
  ADCXQ BP,R12

  MULXQ 16+128(SI),DI,BP
  ADOXQ DI,R12
  ADCXQ BP,R13

  MULXQ 24+128(SI),DI,BP
  ADOXQ DI,R13
  ADCXQ BP,R14

  MULXQ 32+128(SI),DI,BP
  ADOXQ DI,R14
  ADCXQ BP,R15

  MULXQ 40+128(SI),DI,BP
  MOVQ  R9,DX
  ADOXQ DI,R15
  ADCXQ BP,AX
  ADOXQ R8,AX
  ADOXQ R8,R8


  XORQ  R9,R9
  MULXQ 0+128(CX),DI,BP
  ADCXQ 16(SP),DI
  ADOXQ BP,R10

  MULXQ 8+128(CX),DI,BP
  ADCXQ DI,R10
  ADOXQ BP,R11

  MULXQ 16+128(CX),DI,BP
  ADCXQ DI,R11
  ADOXQ BP,R12

  MULXQ 24+128(CX),DI,BP
  ADCXQ DI,R12
  ADOXQ BP,R13

  MULXQ 32+128(CX),DI,BP
  ADCXQ DI,R13
  ADOXQ BP,R14

  MULXQ 40+128(CX),DI,BP
  MOVQ  24(BX),DX
  ADCXQ DI,R14
  ADOXQ BP,R15
  ADCXQ R9,R15
  ADOXQ R9,AX
  ADCXQ R9,AX
  ADOXQ R9,R8
  ADCXQ R9,R8
  MOVQ  R10,16(SP)
  IMULQ 8(SP),R10

  XORQ  R9,R9
  MULXQ 0+128(SI),DI,BP
  ADOXQ DI,R11
  ADCXQ BP,R12

  MULXQ 8+128(SI),DI,BP
  ADOXQ DI,R12
  ADCXQ BP,R13

  MULXQ 16+128(SI),DI,BP
  ADOXQ DI,R13
  ADCXQ BP,R14

  MULXQ 24+128(SI),DI,BP
  ADOXQ DI,R14
  ADCXQ BP,R15

  MULXQ 32+128(SI),DI,BP
  ADOXQ DI,R15
  ADCXQ BP,AX

  MULXQ 40+128(SI),DI,BP
  MOVQ  R10,DX
  ADOXQ DI,AX
  ADCXQ BP,R8
  ADOXQ R9,R8
  ADOXQ R9,R9


  XORQ  R10,R10
  MULXQ 0+128(CX),DI,BP
  ADCXQ 16(SP),DI
  ADOXQ BP,R11

  MULXQ 8+128(CX),DI,BP
  ADCXQ DI,R11
  ADOXQ BP,R12

  MULXQ 16+128(CX),DI,BP
  ADCXQ DI,R12
  ADOXQ BP,R13

  MULXQ 24+128(CX),DI,BP
  ADCXQ DI,R13
  ADOXQ BP,R14

  MULXQ 32+128(CX),DI,BP
  ADCXQ DI,R14
  ADOXQ BP,R15

  MULXQ 40+128(CX),DI,BP
  MOVQ  32(BX),DX
  ADCXQ DI,R15
  ADOXQ BP,AX
  ADCXQ R10,AX
  ADOXQ R10,R8
  ADCXQ R10,R8
  ADOXQ R10,R9
  ADCXQ R10,R9
  MOVQ  R11,16(SP)
  IMULQ 8(SP),R11

  XORQ  R10,R10
  MULXQ 0+128(SI),DI,BP
  ADOXQ DI,R12
  ADCXQ BP,R13

  MULXQ 8+128(SI),DI,BP
  ADOXQ DI,R13
  ADCXQ BP,R14

  MULXQ 16+128(SI),DI,BP
  ADOXQ DI,R14
  ADCXQ BP,R15

  MULXQ 24+128(SI),DI,BP
  ADOXQ DI,R15
  ADCXQ BP,AX

  MULXQ 32+128(SI),DI,BP
  ADOXQ DI,AX
  ADCXQ BP,R8

  MULXQ 40+128(SI),DI,BP
  MOVQ  R11,DX
  ADOXQ DI,R8
  ADCXQ BP,R9
  ADOXQ R10,R9
  ADOXQ R10,R10


  XORQ  R11,R11
  MULXQ 0+128(CX),DI,BP
  ADCXQ 16(SP),DI
  ADOXQ BP,R12

  MULXQ 8+128(CX),DI,BP
  ADCXQ DI,R12
  ADOXQ BP,R13

  MULXQ 16+128(CX),DI,BP
  ADCXQ DI,R13
  ADOXQ BP,R14

  MULXQ 24+128(CX),DI,BP
  ADCXQ DI,R14
  ADOXQ BP,R15

  MULXQ 32+128(CX),DI,BP
  ADCXQ DI,R15
  ADOXQ BP,AX

  MULXQ 40+128(CX),DI,BP
  MOVQ  40(BX),DX
  ADCXQ DI,AX
  ADOXQ BP,R8
  ADCXQ R11,R8
  ADOXQ R11,R9
  ADCXQ R11,R9
  ADOXQ R11,R10
  ADCXQ R11,R10
  MOVQ  R12,16(SP)
  IMULQ 8(SP),R12

  XORQ  R11,R11
  MULXQ 0+128(SI),DI,BP
  ADOXQ DI,R13
  ADCXQ BP,R14

  MULXQ 8+128(SI),DI,BP
  ADOXQ DI,R14
  ADCXQ BP,R15

  MULXQ 16+128(SI),DI,BP
  ADOXQ DI,R15
  ADCXQ BP,AX

  MULXQ 24+128(SI),DI,BP
  ADOXQ DI,AX
  ADCXQ BP,R8

  MULXQ 32+128(SI),DI,BP
  ADOXQ DI,R8
  ADCXQ BP,R9

  MULXQ 40+128(SI),DI,BP
  MOVQ  R12,DX
  ADOXQ DI,R9
  ADCXQ BP,R10
  ADOXQ R11,R10
  ADOXQ R11,R11


  XORQ  R12,R12
  MULXQ 0+128(CX),DI,BP
  ADCXQ 16(SP),DI
  ADOXQ BP,R13

  MULXQ 8+128(CX),DI,BP
  ADCXQ DI,R13
  ADOXQ BP,R14

  MULXQ 16+128(CX),DI,BP
  ADCXQ DI,R14
  ADOXQ BP,R15

  MULXQ 24+128(CX),DI,BP
  ADCXQ DI,R15
  ADOXQ BP,AX

  MULXQ 32+128(CX),DI,BP
  ADCXQ DI,AX
  ADOXQ BP,R8

  MULXQ 40+128(CX),DI,BP
  MOVQ  R13,DX
  ADCXQ DI,R8
  ADOXQ BP,R9
  ADCXQ R12,R9
  ADOXQ R12,R10
  ADCXQ R12,R10
  ADOXQ R12,R11
  ADCXQ R12,R11
  IMULQ 8(SP),DX

  XORQ  R12,R12
  MULXQ 0+128(CX),DI,BP
  ADCXQ DI,R13
  ADOXQ BP,R14

  MULXQ 8+128(CX),DI,BP
  ADCXQ DI,R14
  ADOXQ BP,R15

  MULXQ 16+128(CX),DI,BP
  ADCXQ DI,R15
  ADOXQ BP,AX

  MULXQ 24+128(CX),DI,BP
  ADCXQ DI,AX
  ADOXQ BP,R8
  MOVQ  R15,R13

  MULXQ 32+128(CX),DI,BP
  ADCXQ DI,R8
  ADOXQ BP,R9
  MOVQ  AX,SI

  MULXQ 40+128(CX),DI,BP
  ADCXQ DI,R9
  ADOXQ BP,R10
  MOVQ  R14,DX
  ADCXQ R12,R10
  ADOXQ R12,R11
  LEAQ  128(CX),CX
  MOVQ  R8,R12
  ADCQ  $0,R11




  SUBQ  0(CX),R14
  SBBQ  8(CX),R15
  MOVQ  R9,DI
  SBBQ  16(CX),AX
  SBBQ  24(CX),R8
  SBBQ  32(CX),R9
  MOVQ  R10,BP
  SBBQ  40(CX),R10
  SBBQ  $0,R11

  MOVQ  ret+0(FP), BX
  CMOVQCC R14,DX
  CMOVQCS  R13,R15
  CMOVQCS  SI,AX
  CMOVQCC R8,R12
  MOVQ  DX,0(BX)
  CMOVQCC R9,DI
  MOVQ  R15,8(BX)
  CMOVQCC R10,BP
  MOVQ  AX,16(BX)
  MOVQ  R12,24(BX)
  MOVQ  DI,32(BX)
  MOVQ  BP,40(BX)
  RET
